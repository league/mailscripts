#!/usr/bin/env zsh
## email-tickler-update -- manage a set of 43 mailboxes as a 'tickler' file
## Written by and for Christopher League <league@contrapunctus.net>
## but released to the public domain.

## This program is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

                           ### Settings ###

mail_dir=Maildir

today=$(date +%d)                            # 03, 14, 31, ...
month=$(date +%m-%b)                         # 02-Feb, 11-Nov, ...

day_dir=${mail_dir}/.days.${today}
month_dir=${mail_dir}/.months.${month}

users=($*)

                       ### Helper functions ###

## Maybe run a command; maybe print it instead.  If this script is run
## with DRYRUN set (to anything except empty string), it will avoid
## taking any real actions, and just print the commands.
run() {
    if [[ -z $DRYRUN ]]; then   # do it for real
        $* >/dev/null
    else                        # output only
        print '%' $*
    fi
}

readable() {
    if [[ ! -d $1 ]]; then
        print not a directory: $1
        exit 1
    fi
    if [[ ! -d $1/cur ]]; then
        print not a maildir: $1 '(missing cur/)'
        exit 1
    fi
    if [[ ! -d $1/tmp ]]; then
        print not a maildir: $1 '(missing tmp/)'
        exit 1
    fi
}

                       ### Primary actions ###

protected_move() {
    for m in $1/cur/*(N); do
        run safecat ${user_mail_spool}/tmp ${user_mail_spool}/cur < $m
        run rm -f $m
    done
}

                          ### Main loop ###

for u in $users; do
    user_mail_spool=~$u/${mail_dir}
    readable ${user_mail_spool}
    readable ~$u/${day_dir}
    readable ~$u/${month_dir}
    protected_move ~$u/${day_dir}
    if [[ $today == 01 ]]; then
        protected_move ~$u/${month_dir}
    fi
done

exit 0
